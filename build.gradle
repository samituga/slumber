plugins {
    id 'java'
    id "maven-publish"
}


test {
    useJUnitPlatform()
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "java"

    sourceCompatibility = 17
    targetCompatibility = 17

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation("org.slf4j:slf4j-simple:${slf4jVersion}")

        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/samituga/slumber/")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
    }

    repositories {
        mavenCentral()
    }

    configurations.testImplementation() {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

wrapper {
    gradleVersion = '7.4'
}
